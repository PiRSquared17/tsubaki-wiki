#椿ウヰキ
#Copyright 2010 Yutaka Kachi
#
#2012-12-27 コメント変換、そのまま変換、段落変換でブロック切り出しの位置を変更
#			次の一行が消える不具合の解消と、コードの一貫性のため。
#2012-12-26 コメントタグに対応
#2012-12-26 表組みに対応
#2012-12-25 正規表現のメタ文字「?」がURLに含まれる場合でも、リンクに変換
#			リンクURLが長い場合、リンクテキストを短縮表示
#2010-09-22 そのまま表示変換で、先頭スペースを削除するよう修正
#2010-09-08 ブロック切り出しバグを修正
#2010-09-05b バグ：未対応：段落からブロックを切り出すと、区切りになる行がなくなる。
#2010-09-05a 段落変換に、パターンを引数として渡す。段落のパターン順を調整
#2010-08-16 ファイルを本体と分離



#---- 表記の分析と変換 ----

●表記分析(テキスト配列を)
#wikiページの文書表記をタグに応じて変換。
#タグを追加する場合は、最後の段落分析にも反映すること。
#
#「ブロック切り出し」関数を呼び出す場合は、「テキスト配列の0を配列切り取り」不要
#「ブロック切り出し」関数は、切り出したブロックを戻す。
#そのとき、元のテキスト配列をブロック分だけ削除するため(参照渡しだから)


	変換文とは配列
	(テキスト配列<>空)の間
		対象文＝テキスト配列\0

		#空行
		もし、対象文＝「」ならば、
			変換文に「<p></p>」を配列追加
			テキスト配列の0を配列切り取る
			続ける

		#水平線
		パターン＝「\A----」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			変換文に「<hr>」を配列追加
			テキスト配列の0を配列切り取る
			続ける

		#表タグ表示
		パターン＝「\A\,」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			テキスト配列をパターンでブロック切り出し
			それをパターンで表タグ変換
			それを変換文に配列追加
#			テキスト配列の0を配列切り取る
			続ける

		#一覧表示：箇条書き
		パターン＝「\A\-」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			テキスト配列をパターンでブロック切り出し
			それを「ul」とパターンで一覧変換
			それを変換文に配列追加
#			テキスト配列の0を配列切り取る
			続ける

		#一覧表示：番号付け
		パターン＝「\A\+」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			テキスト配列をパターンでブロック切り出し
			それを「ol」とパターンで一覧変換
			それを変換文に配列追加
#			テキスト配列の0を配列切り取る
			続ける

		#プラグイン表示
		パターン＝「\A#」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象文をパターンでプラグイン変換
			それを変換文に配列追加
			テキスト配列の0を配列切り取る
			続ける

		#コメント文
		パターン＝「\A/」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			テキスト配列をパターンでブロック切り出し
			それをパターンでコメント変換
			それを変換文に配列追加
#			テキスト配列の0を配列切り取る
			続ける

		#そのまま表示
		パターン＝「\A\s」
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			テキスト配列をパターンでブロック切り出し
			それをパターンでまま表示変換
			それを変換文に配列追加
#			テキスト配列の0を配列切り取る
			続ける

		#見出し 1-3レベル
		#正規表現の「{}」を使うため、パターンは『』でくくる
		#{}が、テキスト展開とバッティングするため
		パターン＝『\A\*{1,3}』
		対象文をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象文をパターンとそれで見出し変換
			それを変換文に配列追加
			テキスト配列の0を配列切り取る
			続ける

		#段落
		パターン＝「\A[^-\*\,+/#\s\Z]」　#タグ追加時は、このパターンにも反映する。
#		テキスト配列をパターンで段落変換
		テキスト配列をパターンでブロック切り出し
		それをパターンで段落変換
		それを変換文に配列追加
#		テキスト配列の0を配列切り取る

	変換文で戻る

## 表記変換（ブロック） -----

●表タグ変換(テキスト配列を、区切りで)
	変換文とは文字列
	中間配列とは配列

	テキスト配列を反復
		対象の区切りを「」へ正規表現置換
		中間配列にそれを配列追加

	変換文＝「<table border="1">」
	終端＝「」

	(中間配列＜＞空)の間
		対象文＝中間配列\0

		もし、終端＝「」でなければ
			変換文に終端を一行追加
		対象文をリンク変換
		対象文＝それの「,」を「</td><td>」に置換
		終端＝「</td></tr>」
		変換文に「<tr><td>{対象文}」を一行追加
		中間配列の0を配列切り取る

	もし、終端＝「」でなければ
		変換文に終端を一行追加

	変換文に「</table>」を一行追加
	変換文をコピー
	変換文で戻る


●一覧変換(テキスト配列を、種類と、区切りで)
	変換文とは文字列
	中間配列とは配列

	テキスト配列を反復
		対象の区切りを「」へ正規表現置換
		中間配列にそれを配列追加

	変換文＝「<{種類}>」
	終端＝「」

	(中間配列＜＞空)の間
		対象文＝中間配列\0

		対象文を区切りで正規表現マッチ
		もし、それが空でなければ
			中間配列を区切りでブロック切り出し
			それを種類と区切りで一覧変換
			変換文にそれを一行追加
		違えば
			もし、終端＝「」でなければ
				変換文に終端を一行追加
			終端＝「</li>」
			対象文をリンク変換
			変換文に「<li>{それ}」を一行追加
			中間配列の0を配列切り取る

	もし、終端＝「」でなければ
		変換文に終端を一行追加

	変換文に「</{種類}>」を一行追加
	変換文で戻る

●プラグイン変換(コマンドを、記号で)
	変換文とは文字列
	引数ハッシュとはハッシュ
	個別文とは配列

	引数ハッシュ＠ページ＝処理フォーム＠ページ
	プラグイン文＝コマンドの記号を「」へ正規表現置換
	引数ハッシュ＠プラグイン＝プラグイン文

	パターン＝「\(.*\)\Z」
	プラグイン文をパターンで正規表現マッチ
	もし、それが空ならば
		引数ハッシュ@「コマンド」＝プラグイン文
	違えば
		中間文＝それの「\A\(|\)\Z」を「」へ正規表現置換
		引数ハッシュ@「引数」＝中間文
		個別文＝中間文を「、」で区切る
		個別文を反復
			対象を「＝」で区切る
			引数ハッシュ@「{それ\0}」＝それ\1
		引数ハッシュ@「コマンド」＝プラグイン文の「(」までを切り取る

	変換文＝引数ハッシュ＠「コマンド」を引数ハッシュでプラグイン呼出_内部する
	変換文で戻る

●コメント変換(テキスト配列を、区切りで)
	変換文とは文字列

#	テキスト配列を区切りでブロック切り出し
#	それで反復
	テキスト配列で反復
		それの区切りを「」へ正規表現置換
		変換文にそれを一行追加

	変換文＝「<!-- 」＆変換文＆「 -->」
	変換文で戻る

●まま表示変換(テキスト配列を、区切りで)
	変換文とは文字列

#	テキスト配列を区切りでブロック切り出し
#	それで反復
	テキスト配列で反復
		それの区切りを「」へ正規表現置換
		変換文にそれを一行追加

	変換文＝「<pre>」＆変換文＆「</pre>」
	変換文で戻る

●見出し変換(テキスト文を、見出しパターンと、見出し文字で)
	見出しレベル＝見出し文字の文字数
	テキスト文の見出しパターンを「」へ正規表現置換
	それをリンク変換
	変換文＝「<h{見出しレベル}>{それ}</h{見出しレベル}>」

	変換文で戻る

●段落変換(テキスト配列を、パターンで)
	ブロック文とは配列
	変換文とは文字列

#	ブロック文＝テキスト配列をパターンでブロック切り出し
#	ブロック文＝ブロック文をリンク変換
	ブロック文＝テキスト配列をリンク変換
	変換文＝「<p>」＆ブロック文＆「</p>」

	変換文で戻る


## 表記変換（インライン） --------

●リンク変換(テキスト文を)
	変換文＝「」

	#外部リンクを変換
	URL表記＝「((http|https|mailto):[\x21-\x7E]*)」
	変換文＝テキスト文のURL表記を「\x01$1\x01」へ正規表現置換

	テキスト文からURL表記で正規表現全抽出2
	URL文字数=60
	それを反復
		リンクURL＝それ

		もし、(リンクURLの文字数＞URL文字数）ならば
			リンク語＝リンクURLから(URL文字数)文字左部分＆「...」
		違えば
			リンク語＝リンクURL

		リンク先＝「<a href="{リンクURL}">{リンク語}</a>」
#		変換文＝変換文の「\x01{リンク語}\x01」をリンク先へ正規表現単置換
		変換文＝変換文の「{リンクURL}」をリンク先へ単置換

#		正規表現のメタ文字「?」がURLに含まれる場合でも、リンクに変換
#		正規表現での\x01は、単置換でに対応する - 2010-12-25


	#内部リンクを変換
	テキスト文からリンクタグで正規表現全抽出2
	それを反復
		リンク語＝それ
		リンク先＝リンク語を内部リンク変換
		変換文＝変換文のリンク語をリンク先に置換

	変換文で戻る

●内部リンク変換(リンク語を)
	リンク先＝「」
	リンク語＝リンク語の「[[」を「」に置換
	リンク語＝リンク語の「]]」を「」に置換

	ファイルパス＝DBファイルパス＆「\」＆リンク語

	もし、(ファイルパスの存在＝いいえ)ならば、
		リンク先＝「{リンク語}<a href="{スクリプトURL}?mycmd=edit&mypage={リンク語}">{編集記号}</a>」
	違えば
		リンク先＝「<a href="{スクリプトURL}?{リンク語}">{リンク語}</a>」

	リンク先で戻る


