#椿ウヰキ：アンケートプラグイン用実験プログラム
#アンケート結果の更新後にソート
#
#2011-03-16：103 逆順でソート、テストデータから順位を削除
#2011-03-13：更新にソートを追加
#2011-03-05：更新102
#2011-03-08：更新101
#

アンケート結果文＝「//アンケート結果:ここから
,順位,回答,投票数,グラフ
,,回答１,0,
,,回答２,1,
,,回答３,1,
,,回答４,0,
,,その他,99,」

選択項目＝「回答３」

アンケート結果文を選択項目でアンケート結果_更新
それを表示

●アンケート結果_更新(本文を、項目で)

	本文配列＝本文を改行で区切る

	開始行＝本文配列\0
#	「開始行：{開始行}」を表示
	本文配列の0を配列削除	#開始行マークを削除

	見出行＝本文配列\0
#	「見出行：{見出行}」を表示
	本文配列の0を配列削除	#1行目の見出しを削除

#	「その他 or そのほか」という選択肢の行があれば削除
	他選択肢行＝本文配列で他選択肢行_削除

	本文配列を項目で該当行_更新
	更新行＝それを該当行_ソート
	もし、(他選択肢行=空)でなければ、
		更新行＝更新行＆改行＆他選択肢行
	更新行＝更新行を該当行_番号付け

	結果文とは文字列
	結果文に開始行を一行追加
	結果文に見出行を一行追加
	結果文＝結果文＆更新行

	結果文で戻る


●該当行_更新(本文配列を、項目で)

	#該当行を検出、更新
	結果配列とは配列
	結果文とは文字列

	本文配列を反復
		対象から((対象の文字数)-1)文字右部分
		処理配列＝それを「,」で区切る

		もし、(処理配列\1＝項目)ならば
			処理配列\2＝処理配列\2+1

		更新行＝「{処理配列\1},{処理配列\2}」
		結果文に更新行を一行追加

	結果文で戻る

●該当行_ソート(本文を)

	本文をCSV取得
	それの1を表ソート
	それを配列逆順
	それを表CSV変換

	それで戻る

●該当行_番号付け(本文を)

	結果文とは文字列

	本文を反復
		更新行＝「,{回数},{対象},」
		結果文に更新行を一行追加

	結果文で戻る

●他選択肢行_削除(本文配列で)
#「その他 or そのほか」という選択肢の行があれば削除
	本文_行数＝本文配列の配列要素数
	取得行＝本文配列\(本文_行数-1)
	取得配列＝取得行を「,」で区切る

	もし、(取得配列\2=「その他」)または(取得配列\2=「そのほか」)ならば、
		本文配列の(本文_行数-1)を配列削除
		結果行＝「{取得配列\2},{取得配列\3}」
	違えば
		結果行＝空

	結果行で戻る

