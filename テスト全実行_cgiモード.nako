# なでしこ単体テスト-全テスト実行
#
# 2010-11-06 テストフォルダの指定をtsubaki-config.nakoに移動
# 2010-07-17 椿ウヰキ用にCNAKOモードで実行
# 2010-06-08 ナデシコ付属のtest-all.nakoを改変
#===========================================================
# スルーテスト
#===========================================================
# テスト実行メイン
#※「###テスト省略###」と書かれたファイルは省略する
#

CNAKOモードは「CGI」

!「テスト対象を取り込み.nako」を取り込む
「テストフォルダ：」&テストフォルダを表示
「----全テスト実行！----」を表示。
テスト起動する
「----テスト記録！----」を表示。
テスト記録する

●テスト起動()
    FDIR＝「」
    「{テストフォルダ}*.nako」の全ファイル列挙。
    配列ソート。
#    それを表示
    反復
　　　　FFFテストファイル＝対象
　　　　P＝対象からパス抽出
　　　　もし、P＝テストフォルダならば、続ける。
　　　　もし、FDIR≠Pならば
　　　　　　FDIR=P
　　　　P＝対象からファイル名抽出
　　　　もし、P＝「temp.nako」ならば、続ける。
　　　　FFFテストファイルを開く。
　　　　S=それ。
　　　　NN=Sで「###テスト省略###」が何文字目
　　　　もし(NN>0)ならば、続ける
　　　　N＝Sの関数列挙処理。
　　　　Sをナデシコする。
　　　　Nを反復
　　　　　　FFF関数名＝対象。
　　　　　　FFF関数名をナデシコする。


#===========================================================

●テスト記録()
    Ｔ＝テスト結果取得
    ログ＝テストログ取得。

    ログ＝ログの0から"NG"を表ピックアップ
    結果＝「失敗」
    N_ALL= T@"all"
    N_OK = T@"ok"
    N_NG = T@"ng"
    N_PER = INT(N_OK/N_ALL*100)

    もし（Ｔ＠「ng」＝0）ならば、結果＝「成功」
    ＭＳＧ＝「■テスト結果の報告
    ---
    日付：{今日}、{今}
    なでしこ:{ナデシコバージョン}({ナデシコ最終更新日})
    OS:{OS} → {OSバージョン}
    ---
    テスト結果:{結果}
    テスト成功:{N_OK}/{N_ALL} → {N_PER}%
    テスト失敗:{N_NG}
    ---
    {ログ}」

    もし(結果＝「成功」)ならば
　　　　ＭＳＧをログファイル保存
    違えば
　　　　「■テストに失敗しました。」&改行&
　　　　「---------------------{改行}」＆改行＆
　　　　ＭＳＧをログファイル保存

    終わる。



●関数列挙処理（Ｓの）
　　結果とは変数
　　Ｆとは変数
　　結果＝空
　　Ｓを反復
　　　　Ｆ＝MID(対象,1,4)
　　　　もし(Ｆ = "●テスト")ならば
　　　　　　結果＝結果＆MID(対象,2,999)＆改行
　　それは結果。

●ログファイル保存（Ｓを）
　　ログファイル名＝「test-log.txt」
　　Ｓをログファイル名に保存
#===========================================================


